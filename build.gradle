plugins {
  id 'groovy'
  id 'maven-publish'
  id 'jacoco'
  id 'pl.allegro.tech.build.axion-release' version '1.9.3'
  id 'com.jfrog.artifactory' version '4.8.1'
  id 'com.jfrog.bintray' version '1.8.4'
}

sourceCompatibility = JavaVersion.VERSION_1_5

configurations {
  provided
}

repositories {
  jcenter()
  google()
}

dependencies {
  implementation 'org.codehaus.groovy:groovy-all:2.4.15'
  implementation gradleApi()

  compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

  testImplementation(group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testImplementation 'com.android.tools.build:gradle:3.1.0'
}

scmVersion {
  tag {
    prefix = 'jacoco-android-release'
  }
}

group = 'com.dicedmelon.gradle'
version = scmVersion.version

test {
  testLogging {
    exceptionFormat "full"
  }
}

jacocoTestReport {
  reports {
    html.enabled false
    xml.enabled true
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

publishing {
  publications {
    plugin(MavenPublication) {
      from components.java

      artifact sourcesJar
    }
  }
}

def credentials = (project.hasProperty('bintray.credentials') ?
    project.property('bintray.credentials') :
    System.getenv('BINTRAY_CREDENTIALS') ?: '').split(':')

bintray {
  user = credentials.first()
  key = credentials.last()
  publications = ['plugin']
  pkg {
    repo = 'maven'
    name = "$project.group:$project.name"
    userOrg = 'dicedmelon'
    version {
      gpg {
        sign = true
      }
    }
  }
}

artifactory {
  contextUrl = "http://oss.jfrog.org"
  publish {
    repository {
      repoKey = "oss-snapshot-local"
      username = credentials.first()
      password = credentials.last()
    }
    defaults {
      publications("plugin")
    }
  }
  clientConfig.info.setBuildNumber(System.getProperty("build.number"))
}

task publishSnapshot() {
  if (version.endsWith("-SNAPSHOT")) dependsOn artifactoryPublish
}
